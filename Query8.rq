#+ endpoint: http://192.168.0.1:3030/saam/query
#+ method: POST
#+ summary: Get Artists who have death date on dbpedia but not in this dataset

PREFIX artist: <https://americanart.si.edu/artist/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbpedia: <http://dbpedia.org/resource/>

SELECT DISTINCT ?artist
WHERE {
  ?artist rdf:type schema:Artist ;
          rdf:value ?artist_dbpedia .
  BIND(IRI(?artist_dbpedia) as ?artist_dbpedia_uri)
  SERVICE <http://dbpedia.org/sparql> {
  	?artist_dbpedia_uri a dbo:Artist .
    ?artist_dbpedia_uri dbo:deathDate ?artist_dbpedia_death_date .
  }
  FILTER NOT EXISTS {?artist schema:deathDate ?artist_death_date}
}
LIMIT 10
